! subroutine Compute_Z_Matrix_* ( A, Z )
    real(rk), intent(in) :: A(:,:)    ! Lower triangular activity matrix.
    real(rk), intent(inout) :: Z(:,:) ! Matrix of solution coefficients.
                                      ! Intent(inout) so as not to make the
                                      ! upper triangle undefined, in case the
                                      ! matrix is printed later.
    integer :: I, J, K
    !{ Compute solution coefficients using the recurrence
    ! \begin{equation}\begin{split}
    ! z_{ij} = \,& 0 \text{ if } i \leq j \\
    ! z_{ij} = \,& \frac{a_{ij} - \sum_{k=i}^{i-j-1} a_{i-k,j} \, z_{i,i-k}}
    !               {a_{jj}-a_{ii}} \text{ if } i > j \,.
    ! \end{split}\end{equation}
    ! Elements of Z on and above the diagonal are not referenced and need
    ! not be computed.
    do i = 2, ubound(a,1)
      z(i,1:i-1) = a(i,1:i-1)
      do j = i-1, 1, -1
        if ( a(i,i) == a(j,j) ) then ! Assume both are zero, and assume
                                     ! columns of A sum to zero
          z(i,j) = 0
        else
          do k = 1, i-j-1
            z(i,j) = z(i,j) - a(i-k,j) * z(i,i-k)
          end do
          z(i,j) = z(i,j) / ( a(j,j) - a(i,i) )
        end if
      end do
    end do
! end subroutine Compute_Z_Matrix_*
