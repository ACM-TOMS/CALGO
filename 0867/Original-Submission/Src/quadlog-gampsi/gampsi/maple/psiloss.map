#=======================================================================
# Investigate the bit loss in the asymptotic series for Psi(x).
# Here is the expected output:
#
#        ------------------------------------------------------------------
#        Bit loss occurs if entries in the last column are in (0.5,2)
#
#        x = 11.00000
#         k                      b(k)                  b(k+1)         ratio
#        25          1.5001733492e+23       -9.6899578875e+24        .53382
#        26         -9.6899578875e+24        6.7645882379e+26        .57694
#
#        x = 11.10000
#         k                      b(k)                  b(k+1)         ratio
#        25          1.5001733492e+23       -9.6899578875e+24        .52425
#        26         -9.6899578875e+24        6.7645882379e+26        .56660
#
#        x = 11.20000
#         k                      b(k)                  b(k+1)         ratio
#        25          1.5001733492e+23       -9.6899578875e+24        .51493
#        26         -9.6899578875e+24        6.7645882379e+26        .55652
#
#        x = 11.30000
#         k                      b(k)                  b(k+1)         ratio
#        25          1.5001733492e+23       -9.6899578875e+24        .50585
#        26         -9.6899578875e+24        6.7645882379e+26        .54672
#
#        x = 11.40000
#         k                      b(k)                  b(k+1)         ratio
#        26         -9.6899578875e+24        6.7645882379e+26        .53717
#
#        x = 11.50000
#         k                      b(k)                  b(k+1)         ratio
#        26         -9.6899578875e+24        6.7645882379e+26        .52787
#
#        x = 11.60000
#         k                      b(k)                  b(k+1)         ratio
#        26         -9.6899578875e+24        6.7645882379e+26        .51880
#
#        x = 11.70000
#         k                      b(k)                  b(k+1)         ratio
#        26         -9.6899578875e+24        6.7645882379e+26        .50997
#
#        x = 11.80000
#         k                      b(k)                  b(k+1)         ratio
#        26         -9.6899578875e+24        6.7645882379e+26        .50137
#
#        Maximum bit loss ratio of 0.5 at x = sqrt(c(27)/c(26)/0.5) = 11.81612
#        ------------------------------------------------------------------
#
# [15-Jul-2000]
#=======================================================================
interface(quiet=true):
with(numtheory):			# for B() function

Digits := 75:

b := proc(m)
         RETURN(B(2*m)/(2*m))
     end:

btable := proc(x,show_all)
		local k, ratio, show_x:
		show_x := true:
		for k from 1 to 26
		do
			ratio := (-b(k+1)/b(k))/x^2:
			if (show_all or ((0.5 < ratio) and (ratio < 2))) then
				if (show_x) then
					printf("\nx = %.5f\n", x):
					printf("%2s\t%20s\t%20s\t%10s\n",
						"k", "b(k)", "b(k+1)", "ratio")
				fi:
				show_x := false:
				printf("%2d\t%20.10e\t%20.10e\t%10.5f\n", k, b(k), b(k+1), ratio)
			fi
		od:
	end:

cutoff := 11:	# one below the actual cutoff, to demonstrate where bit loss occurs

printf("------------------------------------------------------------------\n"):
printf("Bit loss occurs if entries in the last column are in (0.5,2)\n"):

for x from cutoff to cutoff+20 by 0.1
do
	btable(x,false)
od:

printf("\nMaximum bit loss ratio of 0.5 at x = sqrt(c(27)/c(26)/0.5) = %.5f\n",
	sqrt(-b(27)/b(26)/0.5)):

printf("------------------------------------------------------------------\n"):

interface(quiet=false):
