#=======================================================================
# Investigate the bit loss in the asymptotic series for Gamma(x).
# Here is the expected output:
#
#	------------------------------------------------------------------
#	Bit loss occurs if entries in the last column are in (0.5,2)
#	
#	x = 12.00000
#	 k                      b(k)                  b(k+1)         ratio
#	27          1.2763374034e+25       -9.2528471761e+26        .50344
#	28         -9.2528471761e+26        7.2188225952e+28        .54179
#	
#	x = 12.10000
#	 k                      b(k)                  b(k+1)         ratio
#	28         -9.2528471761e+26        7.2188225952e+28        .53287
#	
#	x = 12.20000
#	 k                      b(k)                  b(k+1)         ratio
#	28         -9.2528471761e+26        7.2188225952e+28        .52417
#	
#	x = 12.30000
#	 k                      b(k)                  b(k+1)         ratio
#	28         -9.2528471761e+26        7.2188225952e+28        .51568
#	
#	x = 12.40000
#	 k                      b(k)                  b(k+1)         ratio
#	28         -9.2528471761e+26        7.2188225952e+28        .50740
#	
#	Maximum bit loss ratio of 0.5 at x = sqrt(c(29)/c(28)/0.5) = 12.49138
#	------------------------------------------------------------------
#
# [15-Jul-2000]
#=======================================================================
interface(quiet=true):
with(numtheory):			# for B() function

Digits := 75:

b := proc(m)
         RETURN(B(2*m)/(2*m*(2*m-1)))
     end:

btable := proc(x, show_all)
		local k, ratio, show_x:
		show_x := true:
		for k from 1 to 28
		do
			ratio := (-b(k+1)/b(k))/x^2:
			if (show_all or ((0.5 < ratio) and (ratio < 2))) then
				if (show_x) then
					printf("\nx = %.5f\n", x):
					printf("%2s\t%20s\t%20s\t%10s\n",
						"k", "b(k)", "b(k+1)", "ratio")
				fi:
				show_x := false:
				printf("%2d\t%20.10e\t%20.10e\t%10.5f\n", k, b(k), b(k+1), ratio)
			fi
		od:
	end:

cutoff := 12:

printf("------------------------------------------------------------------\n"):
printf("Bit loss occurs if entries in the last column are in (0.5,2)\n"):

for x from cutoff to cutoff+20 by 0.1
do
	btable(x,false)
od:

printf("\nMaximum bit loss ratio of 0.5 at x = sqrt(c(29)/c(28)/0.5) = %.5f\n",
	sqrt(-b(29)/b(28)/0.5)):

printf("------------------------------------------------------------------\n"):

interface(quiet=false):
