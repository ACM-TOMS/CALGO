### -*-maple-*-
########################################################################
### This program investigates Pade approximations to GAMMA(x) in
### [1,2].  It provides functions for computing rational polynomials
### of degree [p,q], estimating their error, and outputting the
### results in tabular form suitable for insertion in Fortran
### programs.
### [23-Jul-2000]
########################################################################

interface(quiet=true):

with(numapprox):

printdata := proc(the_name,c,nc)
		local k, cont:
		printf("      INTEGER n%s\n", the_name):
		printf("      PARAMETER (n%s = %.0f)\n", the_name, evalf(nc)):
		printf("      REAL*16 %s(n%s)\n", the_name, the_name):
		printf("      DATA %s /\n", the_name):
		for k from 1 to nc
		do
			if (evalb(k = nc))
			then
				cont := " /"
			else
				cont := ","
			fi:
			printf("     X  %62.55e%s\n", evalf(c[k]), cont):
		od
	end:

gamapprox1 := proc(y) evalf(subs('x'=y,the_approx)) end:
gamapprox2 := proc(x) evalf(sum('cnum[k]*x^(k-1)', 'k' = 1..nops(the_numer)) /
			    sum('cden[k]*x^(k-1)', 'k' = 1..nops(the_denom))) end:
gamexact   := proc(x) evalf(GAMMA(x)) end:

pq_approx :=
	proc(np, nq)
		global cden, cnum, the_approx, the_denom, the_numer:
		local k, max_relerr, p1, p2, pe, relerr, xxx, x_max_relerr:

		printf("\nAttempting Pade approximation of degree [%d,%d]: ", np, nq):
		the_approx := pade(GAMMA('x'), 'x' = 1.5, [np,nq]):

		## Extract the numerator and denominator polynomials separately
		the_numer := numer(the_approx):
		the_denom := denom(the_approx):

		## pade() does not necessary return the requested
		## degrees, so to avoid duplication of results,
		## discard results that fail to match the requested
		## degrees, sigh...
		if (evalb(nops(the_numer) <> (np + 1)) or
		    evalb(nops(the_denom) <> (nq + 1)))
		then
			printf("FAILED\n"):
			RETURN(FAIL):
		else
			printf("OKAY\n"):
		fi:

		## Maple stupidly lacks a built-in function to
		## retrieve polynomial coefficients in order, so do
		## the job one at a time.
		for k from 1 to nops(the_numer) do cnum[k] := coeff(the_numer,'x',k-1) od:
		for k from 1 to nops(the_denom) do cden[k] := coeff(the_denom,'x',k-1) od:

		## NB: do NOT use the variable x here: it destroys the ones above. Use
		##     xxx instead!

		max_relerr := 0.0:
		x_max_relerr := 1:
		for xxx from 1 to 2 by 0.002
		do
			p2 := gamapprox2(xxx):
			pe := gamexact(xxx):
			if (evalb(pe <> 0))
			then
				relerr := evalf(abs((p2-pe)/pe)):
				if (evalb(max_relerr <= relerr))
				then
					x_max_relerr := xxx:
					max_relerr := relerr:
				fi:
			fi:
		od:

		printf("*\n"):
		printf("*     %s = %.2e\n",
			"Maximum relative error in Gamma(x)", max_relerr):
		printf("*     at x = %.4f for x on [1,2] with Pade degree [%d,%d]\n",
			 x_max_relerr, np, nq):
		printf("*\n"):
		printdata("p", cnum, nops(the_numer)):
		printdata("q", cden, nops(the_denom)):

	end:

Digits := 250; # make sure that this value prints (semicolon, NOT colon)

printf("************************************************************************\n"):
printf("* NB: Remember to change all e exponents to q for use in Fortran!\n"):
printf("************************************************************************\n"):
