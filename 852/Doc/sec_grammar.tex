{\small
\begin{verbatim}
//-----------------------------------------------------------------------------
// A program is a list of pragmas
//-----------------------------------------------------------------------------
First
::=
   Pragma ";" NextPragma

NextPragma
::=
   First
   | epsilon

Pragma
::=
   "Constants" Constants
   | "Variables" Variables
   | "Constraints" Constraints
   | "Consistency" Consistency
   | "Split" Split
   | "Output" Output
   | "Time" "=" Number


//-----------------------------------------------------------------------------
// Management of output
//-----------------------------------------------------------------------------
Output
::=
   OutputArgument NextOutput

NextOutput
::=
   "," Output
   | epsilon

OutputArgument
::=
   "Mode" "=" ("hull" | "union")
   | "Digits" "=" Integer
   | "Style" =" ("bound" | "midpoint")
   | epsilon


//-----------------------------------------------------------------------------
// Consistency techniques
//-----------------------------------------------------------------------------
Consistency
::=
   ConsistencyArgument NextConsistency

NextConsistency
::=
   "," Consistency
   | epsilon

ConsistencyArgument
::=
   "local" "=" ( "bc3" | "bc3_newton" | "bc4" | "bc5" | "hc3" | "hc4" | "hc4I" | "hc4_newton")
   | "strong" "=" ("3b" | "weak3b")
   | "phi" "=" Number
   | "width" "=" Number
   | "improve" = Number
   | epsilon


//-----------------------------------------------------------------------------
// Splitting strategies
//-----------------------------------------------------------------------------
Split
::=
   SplitArgument NextSplit

NextSplit
::=
   "," Split
   | epsilon

SplitArgument
::=
   "none"
   | "precision" "=" Number
   | "choice" "=" ("rr" | "lf" | "mn")
   | "parts" "=" Integer
   | "number" "=" ("+oo" | Integer)
   | "mode" "=" ("paving" | "points")
   | epsilon


//-----------------------------------------------------------------------------
// Definition of constants
//-----------------------------------------------------------------------------
Constants
::=
   Constant NextConstants

NextConstants
::=
   "," Constants
   | epsilon


Constant
::=
   Identifier "=" Expr
   | ConstName "=" "[" Expr "," Expr "]"


//-----------------------------------------------------------------------------
// Definition of variables
//-----------------------------------------------------------------------------
Variables
::=
   OneVariable NextVariables

NextVariables
::=
   "," Variables
   | epsilon

OneVariable
::=
   VarType Identifier VariableArray
   "in"
   BracketBound ExpBound "," ExprBound BracketBound

VariableArray
::=
   "[" Integer "," Integer "]"
   | epsilon

VarType
::=
   "int" | "real" | epsilon

BracketBound
::=
   "[" | "]" | epsilon

ExprBound
::=
   ("pred" | "succ" | epsilon) Expr


//-----------------------------------------------------------------------------
// Definition of constraints
//-----------------------------------------------------------------------------
Constraints
::=
   Constraint NextConstraints




NextConstraints
::=
   "," Constraints
   | epsilon

Constraint
::=
   ("_" Identifier ":" | epsilon) Expr Relation Expr

Relation
::=
   "=" | "<=" | ">=" | "in"


//-----------------------------------------------------------------------------
// Definition of expressions
//-----------------------------------------------------------------------------
Expr
::=
   Expr "+" ExprMul
   | Expr "-" ExprMul
   | ExprMul

ExprMul
::=
   ExprMul "*" ExprExp
   | ExprMul "/" ExprExp
   | ExprUnit Exposant

Exposant
::=
   ( "**" | "^") (Identifier | Integer)     
   | epsilon

ExprUnit
::=
   "(" Expr ")"
   | Identifier
   | Number
   | "-" ExprMul
   | "+" ExprMul
   | "sqrt" "(" Expr ")"
   | "exp" "(" Expr ")"
   | "log" "(" Expr ")"
   | "min" "(" Expr "," Expr ")"
   | "max" "(" Expr "," Expr ")"
   | "cos" "(" Expr ")"
   | "sin" "(" Expr ")"
   | "tan" "(" Expr ")"
   | "cosh" "(" Expr ")"
   | "sinh" "(" Expr ")"
   | "tanh" "(" Expr ")"
   | "acos" "(" Expr ")"
   | "asin" "(" Expr ")"
   | "atan" "(" Expr ")"
   | "acosh" "(" Expr ")"
   | "asinh" "(" Expr ")"
   | "atanh" "(" Expr ")"


//-----------------------------------------------------------------------------
// Identifiers and numbers
//-----------------------------------------------------------------------------
Identifier
::=
   [a-zA-Z@][a-zA-Z0-9_]* IdentArray

IdentArray
::=
   "[" Integer "]"
   | epsilon

Number
::=
   Integer         
   | Float           
   | "+oo"
   | "-oo"

Float
::=
   [0-9]+"."[0-9]*
   | [0-9]+"."[0-9]*("E" | "e")("-" | "+" | epsilon)[0-9]+

Integer
::=
   [0-9]+
\end{verbatim}}
