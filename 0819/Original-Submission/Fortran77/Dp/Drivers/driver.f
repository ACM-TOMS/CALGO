        PROGRAM WRTEST
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC TEST PROGRAM OF AIZ, BIZ: TEST OF WRONSKIAN RELATIONS.   
CCC CHECK THE FOLLOWING RELATIONS FROM 
CCC   ABRAMOWITZ&STEGUN, "HANDBOOK OF MATHEMATICAL FUNCTIONS":
CCC  1.  EQ. 10.4.10
CCC  2.  EQ. 10.4.11
CCC  3.  EQ. 10.4.12 
CCC
CCC  NOTE THAT CHECKS 1,2 MAKE SENSE FOR |ARG(Z)|<=PI/3
CCC  WHILE CHECK 3 IS VALID FOR -PI/3<=ARG(Z)<=PI
CCC 
CCC  4.  ALSO WE TEST ANOTHER WRONSKIAN RELATION FOR BIZ
CCC      WHICH IS VALID IN THE SECTOR PI/3<=ARG(Z)<=PI:
CCC
CCC      W[BI,AI(ZEXP(-I2PI/3))]=EXP(I2PI/3))/2/PI
CCC
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC  WE TEST THE RELATIONS FOR Z=R*EXP(I*PHI) WHERE
CCC               0<=R<=100,  0<=PHI<=PI 
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
        DOUBLE PRECISION PI,SQRT3,X,Y,C,S,AIR,AII,PAIR,PAII,BIR,BII,
     *  PBIR,PBII,U,V,WRAIBI,AR,AI,PAR,PAI,AR1,AI1,APR,API,PAR1,PAI1,
     *  WRM1,WR01,WRAIB2,R,PH,W1MAX,W2MAX,W3MAX,W4MAX
        INTEGER I,J,IFAC,IERR1,IERR2,IERRB
        PI=3.1415926535897932385D0
        SQRT3=1.7320508075688772935D0
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CC THIS IS A WRONSKIAN TEST FOR THE SCALED FUNCTIONS       C
        IFAC=2
CC ONE CAN CHOOSE A WRONSKIAN TEST FOR THE UNSCALED        C 
CC FUNCTIONS BY UNCOMMENTING THE FOLLOWING LINE:           C
CC        IFAC=1                                              
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
        OPEN(UNIT=10,FILE='res1',STATUS='UNKNOWN') 
        WRITE(10,*)' '
        WRITE(10,*)'TEST OF WRONSKIAN RELATIONS FOR AIZ, BIZ' 
        WRITE(10,*)'CHECK THE FOLLOWING RELATION:'
        WRITE(10,*)' '
        WRITE(10,*)'CHECK1. ABRAMOWITZ&STEGUN, 10.4.10'  
        WRITE(10,*)' '   
        WRITE(10,30)'X','Y','CHECK1'
        OPEN(UNIT=11,FILE='res2',STATUS='UNKNOWN') 
        WRITE(11,*)' '
        WRITE(11,*)'TESTS OF WRONSKIAN RELATIONS FOR AIZ' 
        WRITE(11,*)'CHECK THE FOLLOWING RELATION:'
        WRITE(11,*)' '
        WRITE(11,*)'CHECK2. ABRAMOWITZ&STEGUN, 10.4.11'  
        WRITE(11,*)' '   
        WRITE(11,30)'X','Y','CHECK2'
        OPEN(UNIT=12,FILE='res3',STATUS='UNKNOWN') 
        WRITE(12,*)' '
        WRITE(12,*)'FURTHER TESTS OF WRONSKIAN RELATIONS FOR AIZ'
        WRITE(12,*)'CHECK THE FOLLOWING RELATION:'
        WRITE(12,*)'CHECK3. ABRAMOWITZ&STEGUN, 10.4.12'  
        WRITE(12,*)' '   
        WRITE(12,31)'X','Y','CHECK3'
        OPEN(UNIT=14,FILE='res4',STATUS='UNKNOWN') 
        WRITE(14,*)' '
        WRITE(14,*)'ANOTHER TEST OF WRONSKIAN RELATION FOR BIZ:'
        WRITE(14,*)' W[BI,AI(ZEXP(-I2PI/3))]=EXP(I2PI/3))/2/PI '
        WRITE(14,*)'WHICH CAN BE USED IN THE SECTOR PI>|ARG(Z)|>PI/3'
        WRITE(14,*)' '   
        WRITE(14,31)'X','Y','CHECK4'
        W1MAX=0.D0
        W2MAX=0.D0
        W3MAX=0.D0
        W4MAX=0.D0 
        DO 1 I=1,100
          DO 2 J=1,180,2
            IERR1=0
            IERR2=0
            IERRB=0
            R=I
            PH=J*PI/180.D0
            X=R*COS(PH)
            Y=R*SIN(PH)  
            IF (PH.LE.PI/3.D0) THEN
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC          
CCC    CHECK A & S 10.4.10
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC    
              CALL AIZ(1,IFAC,X,Y,AIR,AII,IERR1)
              CALL AIZ(2,IFAC,X,Y,PAIR,PAII,IERR1)
              CALL BIZ(1,IFAC,X,Y,BIR,BII,IERRB)
              CALL BIZ(2,IFAC,X,Y,PBIR,PBII,IERRB) 
              IF ((IERR1.EQ.0).AND.(IERRB.EQ.0)) THEN
                U=PI*((AIR*PBIR-AII*PBII)-(PAIR*BIR-PAII*BII))
                V=PI*((AII*PBIR+AIR*PBII)-(PAII*BIR+PAIR*BII))
                WRAIBI=ABS(SQRT(U*U+V*V)-1.D0)
                IF (WRAIBI.GT.W1MAX) THEN
                  W1MAX=WRAIBI
                ENDIF
                WRITE(10,33)X,Y,WRAIBI
              ENDIF
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC          
CCC    CHECK A & S 10.4.11
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC   
              CALL AIZ(1,IFAC,X,Y,AR,AI,IERR1)
              CALL AIZ(2,IFAC,X,Y,PAR,PAI,IERR1)
              C=-0.5D0
              S=0.5D0*SQRT3
              U=X*C-Y*S
              V=X*S+Y*C
              CALL AIZ(1,IFAC,U,V,AR1,AI1,IERR2)
              CALL AIZ(2,IFAC,U,V,APR,API,IERR2)
              IF ((IERR1.EQ.0).AND.(IERR2.EQ.0)) THEN
                U=APR*C-API*S
                V=APR*S+API*C
                PAR1=U
                PAI1=V
                U=PI*((AR*PAR1-AI*PAI1)-(PAR*AR1-PAI*AI1))-
     *             SQRT3/4.D0
                V=PI*((AR*PAI1+AI*PAR1)-(PAR*AI1+PAI*AR1))+
     *              1.D0/4.D0
                WRM1=SQRT(U*U+V*V)
                IF (WRM1.GT.W2MAX) THEN
                   W2MAX=WRM1
                ENDIF
                WRITE(11,33)X,Y,WRM1
              ENDIF
            ELSE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
CCC   ANOTHER CHECK FOR THE BS   
CCC   IN THE SECTOR ARG(Z)>PI/3   
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
              C=-0.5D0
              S=-0.5D0*SQRT3
              U=X*C-Y*S
              V=X*S+Y*C  
              CALL AIZ(1,IFAC,U,V,AIR,AII,IERR1)
              CALL AIZ(2,IFAC,U,V,PAIR,PAII,IERR1)
              CALL BIZ(1,IFAC,X,Y,BIR,BII,IERRB)
              CALL BIZ(2,IFAC,X,Y,PBIR,PBII,IERRB)
              IF ((IERR1.EQ.0).AND.(IERRB.EQ.0)) THEN
                U=PI*((BIR*PAIR-BII*PAII)-SQRT3*(BII*PAIR+BIR*PAII)+
     *              2.D0*(PBIR*AIR-PBII*AII))-0.5D0
                V=PI*((BII*PAIR+BIR*PAII)+SQRT3*(BIR*PAIR-BII*PAII)+
     *               2.D0*(PBII*AIR+PBIR*AII))+SQRT3*0.5D0
                WRAIB2=SQRT(U*U+V*V)
                IF (WRAIB2.GT.W3MAX) THEN
                  W3MAX=WRAIB2
                ENDIF
                WRITE(14,33)X,Y,WRAIB2
              ENDIF
            ENDIF
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC          
CCC    CHECK A & S 10.4.12
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC   
            CALL AIZ(1,IFAC,X,Y,AR,AI,IERR1)
            CALL AIZ(2,IFAC,X,Y,PAR,PAI,IERR1)
            C=-0.5D0
            S=-0.5D0*SQRT3
            U=X*C-Y*S
            V=X*S+Y*C  
            CALL AIZ(1,IFAC,U,V,AR1,AI1,IERR2)
            CALL AIZ(2,IFAC,U,V,APR,API,IERR2)
            IF ((IERR1.EQ.0).AND.(IERR2.EQ.0)) THEN
              U=APR*C-API*S
              V=APR*S+API*C
              PAR1=U
              PAI1=V        
              U=PI*((AR*PAR1-AI*PAI1)-(PAR*AR1-PAI*AI1))-SQRT3/4.D0
              V=PI*((AR*PAI1+AI*PAR1)-(PAR*AI1+PAI*AR1))-1.D0/4.D0
              WR01=SQRT(U*U+V*V)
              IF (WR01.GT.W4MAX) THEN
                W4MAX=WR01
              ENDIF
              WRITE(12,33)X,Y,WR01
            ENDIF
   2      CONTINUE
 1      CONTINUE
        WRITE(10,*)' '
        WRITE(10,*)'MAXIMUM VALUE OF THE WRONSKIAN CHECK =',W1MAX
        WRITE(11,*)' '
        WRITE(11,*)'MAXIMUM VALUE OF THE WRONSKIAN CHECK =',W2MAX
        WRITE(14,*)' '
        WRITE(14,*)'MAXIMUM VALUE OF THE WRONSKIAN CHECK =',W3MAX
        WRITE(12,*)' '
        WRITE(12,*)'MAXIMUM VALUE OF THE WRONSKIAN CHECK =',W4MAX
 30     FORMAT(6X,A1,18X,A1,12X,A6,11X,A6)                  
 31     FORMAT(6X,A1,18X,A1,12X,A6)
 33     FORMAT(D16.9,1X,D16.9,1X,D16.9)
        END   
