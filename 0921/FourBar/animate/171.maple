#171.maple
#
#   Frank Sottile 
#   Jonathon Hauenstein
#
#  This draws the four-bar linkage interpolating nine
#   given points for solution #171.  This includes and 
#   animation of the linkage positions.  This is the viable 
#   mechanism.  There are two components to the workspace curve
#   with one running through the points in cyclic order, and 
#   the other disjoint from the points.
#
#   It begins with numerical solutions to Problem 4 in the
#   paper of Morgan, Sommese, and Wampler. 
#
#
interface(quiet=true):
with(plots):
with(plottools):
plotsetup(x11):
read("RealSols.maple"):
#
#  Real solutions stored in file as list of Points
#
###########################################################
mr:=proc(r)
 return( trunc(1000000*r)/1000000 )
end proc:
###########################################################
rot:=proc(r,v)
 return([(2*r*v[1] + (r^2-1)*v[2])/(1+r^2),
         (2*r*v[2] - (r^2-1)*v[1])/(1+r^2)])
end proc:
###########################################################
#
#  Interpolating poionts
#
P:=[[0.25, 0.00],
    [0.52, 0.10],
    [0.80, 0.70],
    [1.20, 1.00],
    [1.40, 1.30],
    [1.10, 1.48],
    [0.70, 1.40],
    [0.20, 1.00],
    [0.02, 0.40]]:

P:=[seq([mr(P[ii][1]),mr(P[ii][2])],ii=1..9)]:
Delta:=[seq([P[ii][1]-P[1][1], P[ii][2]-P[1][2]],ii=1..9)]:
Deltac:=[seq(P[ii][1]-P[1][1] + I*P[ii][2]-P[1][2],ii=1..9)]:
        #
i:=171:

S:=[seq(mr(Points[i][ii]),ii=1..12)]:
#Digits:=10:
#S:=[seq(Points[i][ii],ii=1..12)]:

# The four vectors
x:=[S[1],S[2]]:
a:=[S[3],S[4]]:
n:=[S[5],S[6]]:
y:=[S[7],S[8]]:
b:=[S[9],S[10]]:
m:=[S[11],S[12]]:

# Make them into complex numbers
xc:=x[1]+I*x[2]:
ac:=a[1]+I*a[2]:
nc:=n[1]+I*n[2]:
yc:=y[1]+I*y[2]:
bc:=b[1]+I*b[2]:
mc:=m[1]+I*m[2]:
#
#  Check that equation (9) holds.
#
evalf(simplify(ac*conjugate(xc)-nc)):
evalf(simplify(bc*conjugate(yc)-mc)):
#
#  Check that the remaining equations hold
#
for j from 2 to 9 do
solve({(conjugate(ac)-conjugate(Deltac[j]))*xc*gam + (ac-Deltac[j])*conjugate(xc)*cgam + 
         Deltac[j]*(conjugate(ac)-conjugate(xc)) + conjugate(Deltac[j])*(ac-xc)
        -Deltac[j]*conjugate(Deltac[j]),
       (conjugate(bc)-conjugate(Deltac[j]))*yc*gam + (bc-Deltac[j])*conjugate(yc)*cgam + 
         Deltac[j]*(conjugate(bc)-conjugate(yc)) + conjugate(Deltac[j])*(bc-yc)
        -Deltac[j]*conjugate(Deltac[j])},
   {gam,cgam}):
end do:
###########################################################################

# The Anchor points
A:=[P[1][1]+a[1], P[1][2]+a[2]]:
B:=[P[1][1]+b[1], P[1][2]+b[2]]:
#
# Two legs of four-bar mechanism, as vectors. For animation, we rotate
#  the vector u to get a vector ur that is used to get rotated versions 
#  of the different parts of the mechanism
#
u:=[x[1]-a[1], x[2]-a[2]]:
v:=[y[1]-b[1], y[2]-b[2]]:
vr:=rot(0,v):
#
#  The w-bar (as a vector), and its length
#
w:=[y[1]-x[1], y[2]-x[2]]:
Pars:=solve({al*w[1]+be*w[2]=x[1], al*w[2]-be*w[1]=x[2]},{al,be}):

#####################################################################
#
#  p and q are the positions of the endpoints of the u-bar and the v-bar
#
p := (s,t) -> [A[1]+(2*s*t*u[1]+(s^2-t^2)*u[2])/(s^2+t^2), 
               A[2]+(2*s*t*u[2]-(s^2-t^2)*u[1])/(s^2+t^2)]:
q := (s,t) -> [B[1]+(2*s*t*v[1]+(s^2-t^2)*v[2])/(s^2+t^2), 
               B[2]+(2*s*t*v[2]-(s^2-t^2)*v[1])/(s^2+t^2)]:
qr:= (s,t) -> [B[1]+(2*s*t*vr[1]+(s^2-t^2)*vr[2])/(s^2+t^2), 
               B[2]+(2*s*t*vr[2]-(s^2-t^2)*vr[1])/(s^2+t^2)]:
#
#  The one equation is that dist(p,q) = |w|.
#
W:=[q(t,1)[1]-p(s,1)[1],q(t,1)[2]-p(s,1)[2]]:
Wr:=[qr(t,1)[1]-p(s,1)[1],qr(t,1)[2]-p(s,1)[2]]:
X:=simplify(subs(op(Pars),[p(s,1)[1]-al*W[1]-be*W[2], p(s,1)[2]-al*W[2]+be*W[1]])):
Xr:=simplify(subs(op(Pars),[p(s,1)[1]-al*Wr[1]-be*Wr[2], p(s,1)[2]-al*Wr[2]+be*Wr[1]])):

G:=(p(s,1)[1]-q(t,1)[1])^2+(p(s,1)[2]-q(t,1)[2])^2-(w[1]^2+w[2]^2):
G:=numer(simplify(G)):
#implicitplot(G,s=-8..8, t=-8..8,grid=[100,100],color=red);

Gr:=(p(s,1)[1]-qr(t,1)[1])^2+(p(s,1)[2]-qr(t,1)[2])^2-(w[1]^2+w[2]^2):
Gr:=numer(simplify(Gr)):
#implicitplot(Gr,s=-8..8, t=-8..8,grid=[100,100],color=blue);

Gi:=(p(s,1)[1]-q(1,t)[1])^2+(p(s,1)[2]-q(1,t)[2])^2-(w[1]^2+w[2]^2):
Gi:=numer(simplify(Gi)):
#implicitplot(Gi,s=-8..8, t=-8..8,grid=[100,100],color=blue);

As:=coeff(G,s^2): Bs:=coeff(G,s): Cs:=subs(s=0,G):
s1:= unapply((-Bs + sqrt(Bs^2-4*As*Cs))/2/As,t):
s2:= unapply((-Bs - sqrt(Bs^2-4*As*Cs))/2/As,t):

Ars:=coeff(Gr,s^2): Brs:=coeff(Gr,s): Crs:=subs(s=0,Gr):
sr1:= unapply((-Brs + sqrt(Brs^2-4*Ars*Crs))/2/Ars,t):
sr2:= unapply((-Brs - sqrt(Brs^2-4*Ars*Crs))/2/Ars,t):
#####################################################################
x:=unapply(X,s,t):
xr:=unapply(Xr,s,t):

t0:=-2/3:
fa:=[plottools[curve]([A,p(s1(t0),1),q(t0,1),B],color=brown),
     plottools[curve]([p(s1(t0),1),x(s1(t0),t0),q(t0,1)],color=brown)]:

t0:=4:
fb:=[plottools[curve]([A,p(s2(t0),1),q(t0,1),B],color=black),
     plottools[curve]([p(s2(t0),1),x(s2(t0),t0),q(t0,1)],color=black)]:

# For .eps figure
Ground:=pointplot([A,B],symbol=point,color=pink):
Po:=pointplot(P,symbol=point,color=red):

# For animation
Ground:=[pointplot([A,B],symbol=cross,color=black),
     pointplot([A,B],symbol=circle,color=red),
     pointplot([[0,5/2]],symbol=cross, color=black)]:
Po:=pointplot(P,symbol=circle,color=red):

lf:=-1/2: ri:=23/10:
bo:=-3/5: tp:=7/4:
Frame:=plottools[curve]([[lf,bo],[lf,tp],[ri,tp],[ri,bo],[lf,bo]],color=green):


plotR1:=[plot([x(s1(t),t)[], t=-1..1], color=magenta,thickness=2),
         plot([x(s1(1/t),1/t)[], t=-1..1], color=magenta,thickness=2)]:

plotR2:=[plot([x(s2(t),t)[], t=-1..1], color=blue,thickness=2),
         plot([x(s2(1/t),1/t)[], t=-1..1], color=blue,thickness=2)]:

FB:=proc(t)
 local at:
   at:=abs(2*t)-1:
 if t<= 0 then
   display([plottools[curve]([A,p(sr2(at),1),qr(at,1),B],color=black,thickness=2),
            plottools[curve]([p(sr2(at),1),xr(sr2(at),at),qr(at,1)],color=black,thickness=2),
            plottools[curve]([A,p(s1(at),1),q(at,1),B],color=brown,thickness=2),
            plottools[curve]([p(s1(at),1),x(s1(at),at),q(at,1)],color=brown,thickness=2),
            NULL]);
  else
   display([plottools[curve]([A,p(sr2(1/at),1),qr(1,at),B],color=black,thickness=2),
            plottools[curve]([p(sr2(1/at),1),xr(sr2(1/at),1/at),qr(1,at)],color=black,thickness=2),
            plottools[curve]([A,p(s1(1/at),1),q(1,at),B],color=brown,thickness=2),
            plottools[curve]([p(s1(1/at),1),x(s1(1/at),1/at),q(1,at)],color=brown,thickness=2),
            NULL]);
 end if:  
end proc:

Bk:=[plotR1,plotR2,Po,Ground,Frame]:

#   lg: 300 high   y=180
#plotsetup(gif,plotoutput="movie.gif",plotoptions=`height=535,width=535`):
animate(FB,[t],t=-1..1, frames=200, background=display(Bk[]), scaling=constrained,axes=none);
#
#plotsetup(ps,plotoutput=`A.eps`,plotoptions=`color,portrait,width=3in,height=3in,noborder`):
#display(fa,fb,Bk[],scaling=constrained,axes=none);
