      SUBROUTINE COUNT ( C, K, P, N )
      INTEGER C, P, K, N, I, J, JP1, M, MMJ
      DIMENSION C(K), P(N)
C  COUNT COMPUTES THE NUMBER OF PARTITIONS OF AN INTEGER
C  RESTRICTED TO C FOR INTEGERS IN THE RANGE 1 TO N.
C  INPUT:  K -- A POSITIVE INTEGER.
C          C -- AN ARRAY OF K POSITIVE INTEGERS.
C          N -- AN INTEGER LARGER THAN THE MAXIMUM VALUE IN C.
C  OUTPUT: P -- AN ARRAY OF N INTEGERS, WHERE P(M) IS THE
C               NUMBER OF PARTITIONS OF M RESTRICTED TO C.
C  INITIALIZE P
      DO 10 I = 1, N
        P(I) = 0
10    CONTINUE
C  EACH PASS THROUGH THE OUTER LOOP BELOW TRANSFORMS P FROM
C  PARTITIONS RESTRICTED TO C(1), ..., C(I-1) TO
C  PARTITIONS RESTRICTED TO C(1), ..., C(I).
      DO 30 I = 1, K
        J = C(I)
        JP1 = J + 1
        P(J) = P(J) + 1
        DO 20 M = JP1, N
          MMJ = M - J
          P(M) = P(M) + P(MMJ)
20      CONTINUE
30    CONTINUE
      RETURN
      END

