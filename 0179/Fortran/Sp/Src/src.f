      SUBROUTINE MDBETA ( X, P, Q, PROB, IER )
C  FUNCTION - INCOMPLETE BETA PROBABILITY DISTRIBUTION FUNCTION
C
C  USAGE - CALL MDBETA ( X, P, Q, PROB, IER )
C
C  PARAMETERS
C
C    X    - VALUE TO WHICH FUNCTION IS TO BE INTEGRATED.  X
C           MUST BE IN THE RANGE [0,1] INCLUSIVE.
C    P    - INPUT (1ST) PARAMETER.  MUST BE GREATER THAN 0.0.
C    Q    - INPUT (2ND) PARAMETER.  MUST BE GREATER THAN 0.0.
C    PROB - OUTPUT PROBABILITY THAT A RANDOM VARIABLE FROM A
C           BETA DISTRIBUTION HAVING PARAMETERS P AND Q
C           WILL BE LESS THAN OR EQUAL TO X.
C    IER  - ERROR PARAMETER.
C           IER = 0 INDICATES A NORMAL EXIT.
C           IER = 1 INDICATES THAT X IS NOT IN THE RANGE [0,1] INCLUSIVE.
C           IER = 2 INDICATES THAT P AND/OR Q IS LESS THAN OR EQUAL TO 0.
C
      IMPLICIT NONE

      DOUBLE PRECISION ALEPS
      DOUBLE PRECISION C
      DOUBLE PRECISION CNT
      DOUBLE PRECISION D4
      DOUBLE PRECISION DLGAMA
      DOUBLE PRECISION DP
      DOUBLE PRECISION DQ
      DOUBLE PRECISION EPS
      DOUBLE PRECISION EPS1
      DOUBLE PRECISION FINSUM
      INTEGER IB
      INTEGER IER
      DOUBLE PRECISION INFSUM
      INTEGER INT
      DOUBLE PRECISION P
      DOUBLE PRECISION P1
      DOUBLE PRECISION PQ
      DOUBLE PRECISION PROB
      DOUBLE PRECISION PS
      DOUBLE PRECISION PX
      DOUBLE PRECISION Q
      DOUBLE PRECISION TEMP
      DOUBLE PRECISION WH
      DOUBLE PRECISION X
      DOUBLE PRECISION XB
      DOUBLE PRECISION Y
C  DOUBLE PRECISION FUNCTION DLGAMA.
C  MACHINE PRECISION.
      DATA EPS / 2.2D-16 /
C  SMALLEST POSITIVE NUMBER REPRESENTABLE.
      DATA EPS1 / 1.D-78 /
C  NATURAL LOG OF EPS1.
      DATA ALEPS / -179.6016D0 /
C  CHECK RANGES OF THE ARGUMENTS.
      Y = X
      IF ( ( X .LE. 1.D0 ) .AND. ( X .GE. 0.D0 ) ) GO TO 10
      IER = 1
      GO TO 140
10    IF ( ( P .GT. 0.D0 ) .AND. ( Q .GT. 0.D0 ) ) GO TO 20
      IER = 2
      GO TO 140
20    IER = 0
      IF ( X .GT. 0.5D0 ) GO TO 30
      INT = 0
      GO TO 40
C  SWITCH ARGUMENT FOR MORE EFFICIENT USE OF POWER SERIES.
30    INT = 1
      TEMP = P
      P = Q
      Q = TEMP
      Y = 1.D0 - Y
40    IF ( X .NE. 0.D0 .AND. X .NE. 1.D0 ) GO TO 60
C  SPECIAL CASE: X IS 0 OR 1.
50    PROB = 0.D0
      GO TO 130
60    IB = Q
      TEMP = IB
      PS = Q - DBLE ( IB )
      IF ( Q .EQ. TEMP ) PS = 1.D0
      DP = P
      DQ = Q
      PX = DP * DLOG ( Y )
      PQ = DLGAMA ( DP + DQ )
      P1 = DLGAMA ( DP )
      C = DLGAMA ( DQ )
      D4 = DLOG ( DP )
C  DLGAMA IS A FUNCTION WHICH CALCULATES THE DOUBLE
C  PRECISION LOG GAMMA FUNCTION.
      XB = PX + DLGAMA ( PS + DP ) - DLGAMA ( PS ) - D4 - P1
C  SCALING
      IB = XB / ALEPS
      INFSUM = 0.D0
C  FIRST TERM OF A DECREASING SERIES WILL UNDERFLOW.
      IF ( IB .NE. 0 ) GO TO 90
      INFSUM = DEXP ( XB )
      CNT = INFSUM * DP
C  CNT WILL EQUAL DEXP ( TEMP ) * ( 1.D0 - PS ) * I * P * Y**I / FACTORIAL ( I ).
      WH = 0.0D0
80    WH = WH + 1.D0
      CNT = CNT * ( WH - PS ) * Y / WH
      XB = CNT / ( DP + WH )
      INFSUM = INFSUM + XB
      IF ( XB / EPS .GT. INFSUM ) GO TO 80
C  DLGAMA IS A FUNCTION WHICH CALCULATES THE DOUBLE
C  PRECISION LOG GAMMA FUNCTION.
90    FINSUM = 0.D0
      IF ( DQ .LE. 1.D0 ) GO TO 120
      XB = PX + DQ * DLOG ( 1.D0 - Y ) + PQ - P1 - DLOG ( DQ ) - C
C  SCALING
      IB = XB / ALEPS
      IF ( IB .LT. 0 ) IB = 0
      C = 1.D0 / ( 1.D0 - Y )
      CNT = DEXP ( XB - FLOAT ( IB ) * ALEPS )
      PS = DQ
      WH = DQ
100   WH = WH - 1.D0
      IF ( WH .LE. 0.0D0 ) GO TO 120
      PX = ( PS * C ) / ( DP + WH )
      IF ( PX .GT. 1.0D0 ) GO TO 105
      IF ( CNT / EPS .LE. FINSUM .OR. CNT .LE. EPS1 / PX ) GO TO 120
105   CNT = CNT * PX
      IF ( CNT .LE. 1.D0 ) GO TO 110
C  RESCALE
      IB = IB - 1
      CNT = CNT * EPS1
110   PS = WH
      IF ( IB .EQ. 0 ) FINSUM = FINSUM + CNT
      GO TO 100
120   PROB = FINSUM + INFSUM
130   IF ( INT .EQ. 0 ) GO TO 140
      PROB = 1.D0 - PROB
      TEMP = P
      P = Q
      Q = TEMP
140   RETURN
      END
