*COMMENT
FILE 8. THE SIMPLE EXAMPLES FROM THE USER'S GUIDE.
 
 
  A SIMPLE EXAMPLE
*ENDCOM
*SET
       LASTNAME = 'DOE'
       FIRSTNAME = 'JOHN'
       MONTH = 08
       DAY = 24
       YEAR = 81
       SEMESTER = 'FALL'
*ENDSET
*SET ( NCOURSES = 3 )
*SET ( COURSES = 'BIO 255$$/' )
*APPEND ( COURSES, 'GEO 110$$/' )
*APPEND ( COURSES, 'PSY 201' )
*SET ( NAME = '$$LASTNAME, $$FIRSTNAME' )
*SET ( DATE = '$$MONTH/$$DAY/$$YEAR' )
NAME:         $NAME
DATE:         $DATE
SEMESTER:     $SEMESTER
LIST OF COURSES:
*DO ( I = 1, NCOURSES )
   $I.   $LIST(COURSES)
*ENDDO
 
*COMMENT
   EXAMPLE OF $LIST FUNCTION
*ENDCOM
*SET(XYZ = ' A $$/ B+C $$/ D*E $$/ ')
*APPEND (XYZ, ' F-G $$/ HIJ $$/ ')
     X = $LIST(XYZ)
     Y = $LIST(XYZ)
     Z = $LIST(XYZ) - ($LIST(XYZ))
*RESET(XYZ)
     K = $LIST(XYZ)
 
*COMMENT
   EXAMPLE OF ESCAPE CHARACTERS
*ENDCOM
*SET(LINES = '      T = A $$-      A = B $$-      B = T $$-' )
*INCLUDE(LINES)
 
*COMMENT
   EXAMPLE OF *DO STATEMENT
*ENDCOM
*SET( COEFS = '-12.3 $$/ 16.2 $$/ -4.9 $$/' )
*DO (I = 1, 5, 2)
      A($I,1) = B($I) * ($LIST(COEFS))
*ENDDO
 
*COMMENT
   EXAMPLE OF IF'S
*ENDCOM
*OPTION (LCOL1 = .FALSE.)
*SET ( TYPEV = 'VALU' )
*SET ( L1 = 'VALU' )
*SET ( L2 = 6 )
*SET ( L3 = .FALSE. )
*IF  ( L1 = TYPEV )
   A = B
   *IF ( L2 = 6 )
       B = C
       *ELSE
       B = D
   *ENDIF
   *IF ( L3 )      C = D
   *IF ( $DEF(L3)) D = E
*ENDIF
 
*COMMENT
   EXAMPLE OF INCLUDES
*ENDCOM
*OPTION( LSUB = .FALSE. )
*SET
    TIMELABEL= 1000
    TIMER    = .TRUE.
    NAME     = 'SOLVE'
    MATRIX   = 'A'
    SOLUTION = 'U'
    RHS      = 'B'
    NUMBEQNS = 20
*ENDSET
*SET(LINSYSCALL)
   *IF(TIMER)
      *INCLUDE(TIME1)
   *ENDIF
   CALL $NAME($MATRIX,$SOLUTION,$RHS,$NUMBEQNS,WORK,IER)
   *IF(TIMER)
      *INCLUDE(TIME2)
   *ENDIF
*ENDSET
*SET(TIME1)
   CALL SECOND(TIME1)
*ENDSET
*SET(TIME2)
   CALL SECOND(TIME2)
   TIME(KTIME) = TIME2-TIME1
   PRINT $TIMELABEL, TIME(KTIME), '$NAME'
   KTIME = KTIME+1
*ENDSET
*OPTION(LSUB=.TRUE.)
*INCLUDE(LINSYSCALL)
*END
